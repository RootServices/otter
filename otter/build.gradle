apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "net.tokensmith"
description = """Otter"""

sourceCompatibility = 12
targetCompatibility = 12

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/repositories/releases" }
    mavenCentral()
    mavenLocal()
}

configurations {
    deployerJars
}

ext {
    servletApiVersion = '4.0.1'
    jettyVersion = '9.4.29.v20200521'
    jacksonVersion = '2.11.0'
    log4jVersion = '2.13.3'
    slf4jVersion = '1.7.30'
    jwtVersion = '1.3.3'
    translatableVersion = '1.0'
    mockitoVersion = '2.28.2'
}

dependencies {
    compile project (':otter-translatable')
    compile "javax.servlet:javax.servlet-api:${servletApiVersion}"
    compile "org.eclipse.jetty:jetty-annotations:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-webapp:${jettyVersion}"
    compile "org.eclipse.jetty:apache-jsp:${jettyVersion}"
    compile "org.eclipse.jetty:apache-jstl:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-http:${jettyVersion}"
    compile "org.eclipse.jetty.http2:http2-server:${jettyVersion}"
    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    compile "com.fasterxml.jackson.module:jackson-module-afterburner:${jacksonVersion}"

    // bean validation - see RestValidate
    compile "jakarta.validation:jakarta.validation-api:2.0.2"
    compile 'org.hibernate.validator:hibernate-validator:6.1.2.Final'


    compile "org.slf4j:slf4j-simple:${slf4jVersion}"
    compile "org.apache.logging.log4j:log4j-1.2-api:${log4jVersion}"
    compile "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    compile "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    compile "net.tokensmith:jwt:${jwtVersion}"
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.asynchttpclient:async-http-client:2.4.4"
}

test {
    mkdir 'logs/jetty'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'otter'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'otter'
                packaging = 'jar'
                description = 'Micro web framework for servlet api 4.0'
                url = 'https://github.com/tokensmith/otter'

                scm {
                    connection = 'git@github.com:tokensmith/otter.git'
                    developerConnection = 'git@github.com:tokensmith/otter.git'
                    url = 'https://github.com/tokensmith/otter'
                }

                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://github.com/tokensmith/otter/blob/development/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'tmackenzie'
                        name = 'Tom MacKenzie'
                        email = 'tom@tokensmith.net'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            if(!project.ext.properties.containsKey("ossrhUser")) {
                project.ext.ossrhUser = 'wrongUser'
                project.ext.ossrhPassword = 'wrongPassword'
                print "no ossrh user or password"
            }
            credentials {
                username = project.ext.ossrhUser
                password = project.ext.ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

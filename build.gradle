apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group = 'org.rootservices'
archivesBaseName = "otter"
version = '1.1-SNAPSHOT'

description = """Otter"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    deployerJars
}

ext {
    jettyVersion = '9.4.7.v20170914'
    jacksonVersion = '2.8.8'
}

dependencies {
    compile "javax.servlet:javax.servlet-api:3.1.0"
    compile "org.eclipse.jetty:jetty-annotations:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-webapp:${jettyVersion}"
    compile "org.eclipse.jetty:apache-jsp:${jettyVersion}"
    compile "org.eclipse.jetty:apache-jstl:${jettyVersion}"
    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version:'2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version:'2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version:'2.7'
    compile group: 'org.rootservices', name: 'jwt', version:'1.2'
    compile group: 'org.rootservices', name: 'otter-translatable', version:'1.0'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
    testCompile group: 'com.ning', name: 'async-http-client', version:'1.9.15'
}

test {
    mkdir 'logs/jetty'
    filter {
        includeTestsMatching "*IntegrationTestSuite"
        includeTestsMatching "*UnitTestSuite"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            if(!project.ext.properties.containsKey("ossrhUser")) {
                project.ext.ossrhUser = 'wrongUser'
                project.ext.ossrhPassword = 'wrongPassword'
            }

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            pom.project {
                groupId 'org.rootservices'
                artifactId project.name
                version project.version

                description project.description
                packaging 'jar'
                url 'https://github.com/RootServices/otter'
            }

            pom.project {
                name project.name
                packaging 'jar'
                description 'A micro web framework'
                url 'https://github.com/RootServices/otter'

                scm {
                    connection 'git@github.com:RootServices/otter.git'
                    developerConnection 'git@github.com:RootServices/otter.git'
                    url 'https://github.com/RootServices/otter'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://github.com/RootServices/otter/blob/development/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'tmackenzie'
                        name 'Tom MacKenzie'
                        email 'tom@rootservices.org'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}

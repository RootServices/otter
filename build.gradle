apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'

group = 'org.rootservices'
version = '1.2-SNAPSHOT'

description = """Otter"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

configurations {
    deployerJars
}

ext {
    jettyVersion = '9.4.8.v20171121'
    jacksonVersion = '2.8.8'
    log4jVersion = '2.7'
}

dependencies {
    compile "javax.servlet:javax.servlet-api:4.0.0"
    compile "org.eclipse.jetty:jetty-annotations:${jettyVersion}"
    compile "org.eclipse.jetty:jetty-webapp:${jettyVersion}"
    compile "org.eclipse.jetty:apache-jsp:${jettyVersion}"
    compile "org.eclipse.jetty:apache-jstl:${jettyVersion}"
    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile "org.apache.logging.log4j:log4j-1.2-api:${log4jVersion}"
    compile "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    compile "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    compile group: 'org.rootservices', name: 'jwt', version:'1.2'
    compile group: 'org.rootservices', name: 'otter-translatable', version:'1.0'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:'1.3'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
    testCompile group: 'com.ning', name: 'async-http-client', version:'1.9.15'
}

test {
    mkdir 'logs/jetty'
    filter {
        includeTestsMatching "*IntegrationTestSuite"
        includeTestsMatching "*UnitTestSuite"
    }
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            if(!project.ext.properties.containsKey("ossrhUser")) {
                project.ext.ossrhUser = 'wrongUser'
                project.ext.ossrhPassword = 'wrongPassword'
            }

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUser, password: ossrhPassword)
            }
            pom.project {
                groupId 'org.rootservices'
                artifactId project.name
                version project.version
                name project.name
                description project.description
                packaging 'jar'
                url 'https://github.com/RootServices/otter'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}
